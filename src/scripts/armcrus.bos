#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, turret, barrel1, flare1, barrel2, flare2, wake1, wake2,
       foreturret, foregun, foreflare, aftturret, aftgun, aftflare;

static-var  gun_1, gun_1_yaw;

// Signal definitions
#define SIG_AIM_3			2
#define SIG_AIM_2			4
#define SIG_AIM				8
#define SIG_MOVE			16
#define SIG_ROCK_Z				32		//signal to prevent multiple rocking

#define RESTORE_DELAY_P			3000
#define RESTORE_DELAY_S			3000

#define TURRET_PITCH_SPEED_1	<90>
#define TURRET_YAW_SPEED_1		<110>
#define RECOIL_DISTANCE_1		[-2]
#define RECOIL_RESTORE_SPEED_1	[1]

//rockz
#define ROCK_PIECE				base	//piece to rock
#define ROCK_Z_SPEED			3		//number of quarter-cycles per second around z-axis
#define ROCK_Z_DECAY			-1/2	//rocking around z-axis is reduced by this factor each time; should be negative to alternate rocking direction
#define ROCK_Z_MIN              <0.5>	//if around z-axis rock is not greater than this amount rocking will stop after returning to center

#include "rockz.h"

#define ROCK_Z_FIRE_1			-5

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

Create()
{
	hide flare1;
	hide flare2;
	hide foreflare;
	hide aftflare;
	gun_1 = 1;
	start-script SmokeUnit();
}

RestoreAfterDelay()
{
	sleep RESTORE_DELAY_P;
	set-signal-mask 0;
	turn turret to y-axis <0.000000> speed <55.000000>;
	turn barrel1 to x-axis <0.000000> speed <15.000000>;
	turn barrel2 to x-axis <0.000000> speed <15.000000>;
}

RestoreDeckLaser()
{
	sleep RESTORE_DELAY_S;
	set-signal-mask 0;
	turn foreturret to y-axis <0.000000> speed <150.000000>;
	turn foregun to x-axis <0.000000> speed <100.000000>;
	turn aftturret to y-axis <180.000000> speed <150.000000>;
	turn aftgun to x-axis <0.000000> speed <100.000000>;
}

StartMoving()
{
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
	while( TRUE )
	{
		emit-sfx 2 from wake1;
		emit-sfx 2 from wake2;
		sleep 300;
	}
}

StopMoving()
{
	signal SIG_MOVE;
}

//---------------------------------------------------------------
//------ WEAPON 1 -----------------------------------------------
//---------------------------------------------------------------

AimPrimary(heading, pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	turn turret to y-axis heading speed TURRET_YAW_SPEED_1;
	turn turret to x-axis <0.000000> - pitch speed TURRET_PITCH_SPEED_1;
	wait-for-turn turret around y-axis;
	wait-for-turn turret around x-axis;
	start-script RestoreAfterDelay();
	gun_1_yaw = heading;
	return (1);
}

AimFromPrimary(piecenum)
{
	piecenum = turret;
}

QueryPrimary(piecenum)
{
	if( gun_1 == 1 ) {
		piecenum = flare1;
	} else {
		piecenum = flare2;
	}
}

FireWeapon1()
{
	gun_1 = 0;
	start-script RockZ(ROCK_Z_FIRE_1, gun_1_yaw);
}

Shot1()
{
	++gun_1;
	if( gun_1 == 1 )
	{
		move barrel1 to z-axis RECOIL_DISTANCE_1 now;	
		wait-for-move barrel1 along z-axis;
		move barrel1 to z-axis [0.000000] speed RECOIL_RESTORE_SPEED_1;
	}
	else
	{
		move barrel2 to z-axis RECOIL_DISTANCE_1 now;	
		wait-for-move barrel2 along z-axis;
		move barrel2 to z-axis [0.000000] speed RECOIL_RESTORE_SPEED_1;
	}
}



//---------------------------------------------------------------
//------ WEAPON 2+3 ---------------------------------------------
//---------------------------------------------------------------

AimSecondary(heading, pitch)
{
	signal SIG_AIM_2;
	set-signal-mask SIG_AIM_2;
	turn foreturret to y-axis heading speed <300.000000>;
	turn foregun to x-axis <0.000000> - pitch speed <200.000000>;
	wait-for-turn foreturret around y-axis;
	wait-for-turn foregun around x-axis;
	start-script RestoreDeckLaser();
	return (1);
}

AimFromSecondary(piecenum)
{
	piecenum = foregun;
}

QuerySecondary(piecenum)
{
	piecenum = foreflare;
}

FireSecondary()
{
	return (0);
}

Shot2()
{
	emit-sfx 1024 from foregun;
	emit-sfx 1025 from foreflare;
}

AimTertiary(heading, pitch)
{
	signal SIG_AIM_3;
	set-signal-mask SIG_AIM_3;
	turn aftturret to y-axis heading speed <300.000000>;
	turn aftgun to x-axis <0.000000> - pitch speed <200.000000>;
	wait-for-turn aftturret around y-axis;
	wait-for-turn aftgun around x-axis;
	start-script RestoreDeckLaser();
	return (1);
}

AimFromTertiary(piecenum)
{
	piecenum = aftgun;
}

QueryTertiary(piecenum)
{
	piecenum = aftflare;
}

FireTertiary()
{
	return (0);
}

Shot3()
{
	emit-sfx 1024 from aftgun;
	emit-sfx 1025 from aftflare;
}



//---------------------------------------------------------------
//------ WEAPON 4 -----------------------------------------------
//---------------------------------------------------------------

AimWeapon4(heading, pitch)
{
	return (1);
}

AimFromWeapon4(piecenum)
{
	piecenum = base;
}

QueryWeapon4(piecenum)
{
	piecenum = base;
}

FireWeapon4()
{
	sleep 150;
}



//---------------------------------------------------------------
//------ KILLED -------------------------------------------------
//---------------------------------------------------------------

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	hide flare1;
	hide flare2;
	hide foreflare;
	hide aftflare;
	if( severity <= 25 )
	{
		corpsetype = 1;
		explode barrel1 type BITMAPONLY | BITMAP1;
		explode barrel2 type BITMAPONLY | BITMAP1;
		explode base type BITMAPONLY | BITMAP3;
		explode turret type BITMAPONLY | BITMAP1;
		explode foreturret type BITMAPONLY | BITMAP2;
		explode aftturret type BITMAPONLY | BITMAP1;
		explode foregun type BITMAPONLY | BITMAP3;
		explode aftgun type BITMAPONLY | BITMAP1;
		explode wake1 type BITMAPONLY | BITMAP2;
		explode wake2 type BITMAPONLY | BITMAP3;
		return (0);
	}
	if( severity <= 50 )
	{
		corpsetype = 2;
		explode barrel1 type FALL | EXPLODE_ON_HIT | BITMAP1;
		explode barrel2 type FALL | EXPLODE_ON_HIT | BITMAP1;
		explode base type BITMAPONLY | BITMAP3;
		explode turret type SHATTER | EXPLODE_ON_HIT | BITMAP1;
		explode foreturret type SHATTER | EXPLODE_ON_HIT | BITMAP2;
		explode aftturret type SHATTER | EXPLODE_ON_HIT | BITMAP1;
		explode foregun type FALL | EXPLODE_ON_HIT | BITMAP3;
		explode aftgun type FALL | EXPLODE_ON_HIT | BITMAP2;
		explode wake1 type FALL | EXPLODE_ON_HIT | BITMAP2;
		explode wake2 type FALL | EXPLODE_ON_HIT | BITMAP3;
		return (0);
	}
	corpsetype = 3;
	explode barrel1 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
	explode barrel2 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
	explode base type BITMAPONLY | BITMAP3;
	explode turret type SHATTER | EXPLODE_ON_HIT | BITMAP1;
	explode foreturret type SHATTER | EXPLODE_ON_HIT | BITMAP2;
	explode aftturret type SHATTER | EXPLODE_ON_HIT | BITMAP1;
	explode foregun type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
	explode aftgun type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
	explode wake1 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
	explode wake2 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
}
