piece head,hips,lshoulder,lbcover,ltcover,rshoulder,rbcover,rtcover,chest,rthigh,lthigh,lshin,rshin,rfoot,lfoot,lmissile,rmissile,rmuzzle,rexhaust,lmuzzle,lexhaust;

static-var isMoving, isAiming, shooting_num, gun;

// Signal definitions
#define SIG_AIM				2
#define SIG_AIM_2			4
#define SIG_MOVE			16

#include "sfxtype.h"
#include "exptype.h"		

DamageControl()
{
	while (get BUILD_PERCENT_LEFT) {
		sleep 1000;
	}
	var health;
	while (TRUE)
	{
		health = get HEALTH;
		if (health < 50)
		{
			emit-sfx SFXTYPE_WHITESMOKE from hips;
			if (health < 25)
			{	
				emit-sfx SFXTYPE_BLACKSMOKE from chest;
			}
		}
		sleep 1000;
	}
}

walk()
{
	turn rthigh 	to y-axis <0> 		speed <135>;
	turn lthigh 	to y-axis <0> 		speed <130>;
	
	turn rthigh 	to z-axis <0> 		speed <135>;
	turn lthigh 	to z-axis <0> 		speed <130>;
	turn lfoot 	to z-axis <0> 		speed <130>;
	turn rfoot 	to z-axis <0> 		speed <130>;
			
	if (isMoving)
	{
			Turn rshin to x-axis <85> speed<540>;	
			Turn rthigh to x-axis <-100> speed<270>;
			Turn lthigh to x-axis <30> speed<270>;
			wait-for-move hips along y-axis;		
			move hips to y-axis [1.2] speed <8400>;
			wait-for-move hips along y-axis;
			turn rshin to x-axis <10> speed <630>;
			move hips to y-axis [0] speed <8400>;
	}
	if (isMoving)
	{
			Turn lshin to x-axis <85> speed<540>;
			Turn lthigh to x-axis <-100> speed<270>;
			turn rthigh to x-axis <30> speed <270>;
			wait-for-move hips along y-axis;
			move hips to y-axis [1.2] speed <8400>;	
			wait-for-move hips along y-axis;
			turn lshin to x-axis <10> speed <630>;
			move hips to y-axis [0] speed <8400>;
	}
}


shootwalk()
{
	turn rthigh 	to y-axis <0> 		speed <135>;
	turn lthigh 	to y-axis <0> 		speed <130>;
	
	turn rthigh 	to z-axis <0> 		speed <135>;
	turn lthigh 	to z-axis <0> 		speed <130>;
	turn lfoot 	to z-axis <0> 		speed <130>;
	turn rfoot 	to z-axis <0> 		speed <130>;
		
	if (isMoving)
	{
			Turn rshin to x-axis <85> speed<540>;	
			Turn rthigh to x-axis <-100> speed<270>;
			Turn lthigh to x-axis <30> speed<270>;
			wait-for-move hips along y-axis;		
			move hips to y-axis [1.2] speed <8400>;
			wait-for-move hips along y-axis;
			turn rshin to x-axis <10> speed <630>;
			move hips to y-axis [0] speed <8400>;
	}		
	if (isMoving)
	{
			Turn lshin to x-axis <85> speed<540>;
			Turn lthigh to x-axis <-100> speed<270>;
			turn rthigh to x-axis <30> speed <270>;
			wait-for-move hips along y-axis;
			move hips to y-axis [1.2] speed <8400>;	
			wait-for-move hips along y-axis;
			turn lshin to x-axis <10> speed <630>;
			move hips to y-axis [0] speed <8400>;
	}
}

stopwalk ()
{
	turn lfoot		to x-axis <0> 		speed <395>;
	turn rfoot		to x-axis <0> 		speed <395>;
	turn rthigh 	to x-axis <0> 		speed <235>;
	turn lthigh 	to x-axis <0> 		speed <230>;
	turn lshin  	to x-axis <0> 		speed <235>;
	turn rshin 	to x-axis <0> 		speed <230>;
	
	turn rthigh 	to y-axis <-20> 	speed <135>;
	turn lthigh 	to y-axis <20> 		speed <130>;
	
	turn rthigh 	to z-axis <3> 		speed <135>;
	turn lthigh 	to z-axis <-3> 		speed <130>;
	turn lfoot 	to z-axis <3> 		speed <130>;
	turn rfoot 	to z-axis <-3>		speed <130>;
}



StartMoving()
{
	isMoving = TRUE;
}

StopMoving()
{
	isMoving = FALSE;
}

MotionControl()
{
	while ( TRUE )
	{              
		IF (isMoving)
		{
			IF (isAiming)
			{
				CALL-SCRIPT shootwalk();
			}
                else
			{
				CALL-SCRIPT walk();
			}
		}
		else
		{
			CALL-SCRIPT stopwalk();
			sleep 120; 
		}
	}
}

Create()
{
	isMoving = FALSE;
	isAiming = FALSE;
	shooting_num=1;
	hide rmuzzle;
	hide lmuzzle;
	hide rexhaust;
	hide lexhaust;
	start-script MotionControl();	
	start-script DamageControl();
}
	
SweetSpot(piecenum)
{
	piecenum=chest;
}	
	
AimFromWeapon3(piecenum)
{
	piecenum=chest;
}

QueryWeapon3(piecenum)
{
	if( gun == 0 )
	{
		piecenum = lmissile;
	}
	if( gun == 1 )
	{
		piecenum = rmissile;
	}
}

AimFromWeapon1(piecenum)
{
	piecenum=chest;
}
	
AimFromWeapon2(piecenum)
{
	piecenum=chest;
}		
//---------------------------------------------------------------------
//gun functions;
//---------------------------------------------------------------------	
RestoreAfterDelay()
{
	sleep 2000;
	turn chest to y-axis <0> speed <100>;
	turn rtcover to x-axis <0> speed <100>;
	turn rbcover to x-axis <0> speed <100>;
	turn ltcover to x-axis <0> speed <100>;
	turn lbcover to x-axis <0> speed <100>;
	isAiming = FALSE;
}

AimWeapon3(heading,pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	isAiming = TRUE;
	turn hips to x-axis <0> now;
	turn chest to x-axis <0> now; 
	turn chest to y-axis heading speed <450>;
	turn lshoulder to x-axis <0> - pitch speed <500>;
	turn rshoulder to x-axis <0> - pitch speed <500>;
	turn rtcover to x-axis <-100> speed <400>;
	turn rbcover to x-axis <100> speed <400>;
	turn ltcover to x-axis <-100> speed <400>;
	turn lbcover to x-axis <100> speed <400>;
	wait-for-turn chest around y-axis;
	wait-for-turn lshoulder around x-axis;
	start-script RestoreAfterDelay();
	
	return(TRUE);
}
	
FireWeapon3()
{
	if (gun == 1)
	{
		emit-sfx 1024 from lmuzzle;
		emit-sfx 1025 from lexhaust;
		move lmissile to z-axis [-1] now;	
		move lmissile to z-axis [0] speed <500>;
		gun = 0;
	}
	else
	{
		emit-sfx 1024 from rmuzzle;
		emit-sfx 1025 from rexhaust;
		move rmissile to z-axis [-1] now;
		move rmissile to z-axis [0] speed <500>;
		gun = 1;
	}
}
	

Killed( severity, corpsetype )
{
	if (severity <= 25)
	{
		corpsetype = 1;
		explode hips type		SHATTER;
		explode chest type		SHATTER;
		explode lshoulder type	SHATTER;
		explode rshoulder type	SHATTER;
		explode head type		FALL | FIRE | BITMAP2;
		return( 0 );
	}

	if (severity <= 50)
	{
		corpsetype = 2;
		explode hips type		SHATTER;
		explode chest type		SHATTER;
		explode lshoulder type	FALL | FIRE | BITMAP2;
		explode rshoulder type	FALL | FIRE | BITMAP2;
		explode head type		FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
		return( 0 );
	}

	corpsetype = 3;
	explode hips type		SHATTER;
	explode chest type		SHATTER;
	explode lshoulder type	SHATTER;
	explode rshoulder type	SHATTER;
	explode head type		FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
	
	return( 0 );
}